plugins {
    id 'com.github.hierynomus.license' version '0.16.1'
    id "com.github.sherter.google-java-format" version "0.9"
    id "net.ltgt.errorprone" version "3.1.0"
    id 'org.springframework.boot' version '2.7.15'
}

apply plugin: 'java'
apply plugin: 'maven-publish'
apply plugin: 'com.github.sherter.google-java-format'
apply plugin: 'io.spring.dependency-management'

googleJavaFormat {
    toolVersion '1.5'
    include '**/*.java'
    exclude '**/generated-sources/*'
}

group = 'com.uber'
version = '0.0.1'

description = """Samples for Cadence. Based on samples for AWS SWF"""

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
    // replace with mavenLocal() if you want to test local change from Cadence Java Client (use publishToMarvenLocal to override)
    mavenCentral()
}

dependencies {
    errorprone("com.google.errorprone:error_prone_core:2.23.0")
    implementation group: 'com.uber.cadence', name: 'cadence-client', version: '3.7.2'
    implementation group: 'commons-configuration', name: 'commons-configuration', version: '1.9'
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'
    implementation group: 'com.uber.m3', name: 'tally-core', version: '0.10.0'
    implementation group: 'com.uber.m3', name: 'tally-prometheus', version: '0.10.0'
    implementation group: 'io.prometheus', name: 'simpleclient', version: '0.10.0'
    implementation group: 'io.prometheus', name: 'simpleclient_httpserver', version: '0.10.0'
    testImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-all', version: '1.10.19'
    testImplementation group: 'org.powermock', name: 'powermock-api-mockito', version: '1.7.4'
    implementation 'org.springframework.boot:spring-boot-starter-web'
}

compileJava {
    dependsOn 'googleJavaFormat'
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xdoclint:none"
    options.errorprone.errorproneArgs = ["-XepExcludedPaths:.*/generated-sources/.*"]
    options.warnings = false
}

task execute(type: JavaExec) {
    main = findProperty("mainClass") ?: ""
    classpath = sourceSets.main.runtimeClasspath
}

license {
    header rootProject.file('license-header.txt')
    include "**/*.java"
}
